; Script generated by the Inno Setup Script Wizard...
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

; Modificado por pam Feb 2024   

; Actualizar estos datos para las actualizaciones

#define MyExeSetup "TRFactura_2.0.3_240229"
#define MyAppDate "29 Feb 2024"
#define MyAppVersion "2.0.3"

#define MyAppName "TR Facturación"
#define MyAppPublisher "Legal Publishing"
#define MyAppURL "http://www.legalpublishing.cl//"
#define MyAppExeName "TRFacturacion.exe"
#define MyAppIconName "TR Facturación"
#define ChkBase "\Datos\TRFactura.ldb"

#define WinSys "..\libs"

; #expr Exec("D:\Projects\signProg.bat", "Archivos\" + MyAppExeName, SourcePath )

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{0F04D1A7-22AB-40E9-885B-562A8922DB71}

AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
; DefaultDirName={sd}HR\LPContab
DefaultDirName=HR\TRFactura
DisableDirPage=no
DefaultGroupName={#MyAppName}
; LicenseFile=..\..\Licencia\LicenciaLPContab.rtf
OutputDir=.
OutputBaseFilename={#MyExeSetup}
SetupIconFile=Images\OpenFact.ico
Compression=lzma/ultra
SolidCompression=true
VersionInfoCompany={#MyAppPublisher}
VersionInfoCopyright={#MyAppPublisher}
VersionInfoDescription=Actualización {#MyAppName}
InternalCompressLevel=ultra
MinVersion=6.1
AppCopyright={#MyAppPublisher}
ShowLanguageDialog=no
WizardImageBackColor=clMaroon
UninstallFilesDir={app}\Uninstall
WindowVisible=false
BackColor=clMaroon
WizardSmallImageFile=Images\TRFacturaIco.bmp
WizardImageFile=Images\TRFactura1.bmp
PrivilegesRequired=lowest
ExtraDiskSpaceRequired=100000
EnableDirDoesntExistWarning=true
DirExistsWarning=no
AllowNetworkDrive=yes
AllowUNCPath=yes
; SignTool=Sign

[Languages]
Name: spanish; MessagesFile: compiler:Languages\Spanish.isl

[Tasks]
; Name: desktopicon; Description: {cm:CreateDesktopIcon}; GroupDescription: {cm:AdditionalIcons}; Flags: unchecked
Name: desktopicon; Description: {cm:CreateDesktopIcon}; GroupDescription: {cm:AdditionalIcons}
Name: quicklaunchicon; Description: {cm:CreateQuickLaunchIcon}; GroupDescription: {cm:AdditionalIcons}; Flags: unchecked; OnlyBelowVersion: 0,6.1

[Files]
Source: Archivos\{#MyAppExeName}; DestDir: {app}; Flags: replacesameversion; Tasks: ; Languages: 
; Source: Archivos\LPContab.chm; DestDir: {app}
Source: Archivos\LicenciaLPContab.pdf; DestDir: {app}
Source: {#WinSys}\FwZip32.dll; DestDir: {app}; Flags: replacesameversion 32bit
Source: {#WinSys}\FairDll32.dll; DestDir: {app}; Flags: replacesameversion 32bit

[Icons]
Name: {group}\{#MyAppName}; Filename: {app}\{#MyAppExeName}
Name: {group}\{cm:UninstallProgram, {#MyAppName}}; Filename: {uninstallexe}
; Name: {group}\Ayuda {#MyAppName}; Filename: {app}\FairPay2.chm; Tasks: ; Languages:
Name: {group}\Licencia {#MyAppName}; Filename: {app}\LicenciaLPContab.pdf; Tasks: ; Languages: 
Name: {commondesktop}\{#MyAppName}; Filename: {app}\{#MyAppExeName}; Tasks: desktopicon
Name: {userappdata}\Microsoft\Internet Explorer\Quick Launch\{#MyAppName}; Filename: {app}\{#MyAppExeName}; Tasks: quicklaunchicon

[Run]
Filename: {app}\{#MyAppExeName}; Flags: nowait postinstall skipifsilent runasoriginaluser; Parameters: hola; WorkingDir: {app}; Description: Ejecutar {#MyAppName}

[Messages]
WelcomeLabel1=Bienvenido al asistente de instalación de la actualización de [name]
WelcomeLabel2=Este programa instalará la actualización de [name/ver] en su sistema ({#MyAppDate}). %n%nAntes de continuar verifique que {#MyAppName} esté cerrado y que no haya otros usuarios usando este programa.%n%nPara poder utilizar esta actualización, es necesario que previamente haya realizado la instalación completa (demo). %n%nSe recomienda que cierre todas las aplicaciones antes de continuar.%n%n

[InstallDelete]
Name: {app}\TRFacturacion.exe; Type: files; Tasks: ; Languages: 
Name: {commondesktop}\Demo de {#MyAppName}.lnk; Type: files

[Code]
// Para verificar que la aplicación no se está ejecutando
const WM_CLOSE = 16;

Function InitializeSetup : Boolean;
var winHwnd: longint;
    retVal : boolean;
    strProg: string;
begin
  Result := true;
  try
    //Either use FindWindowByClassName. ClassName can be found with Spy++ included with Visual C++.
    strProg := '{#MyAppName}';
    // winHwnd := FindWindowByClassName(strProg);
    //Or FindWindowByWindowName.  If using by Name, the name must be exact and is case sensitive.
//    strProg := 'Untitled - Notepad';
    winHwnd := FindWindowByWindowName(strProg);
    Log('winHwnd: ' + inttostr(winHwnd));
    if winHwnd <> 0 then
		if MsgBox('La aplicación "{#MyAppName}" se debe cerrar antes de proceder con la actualización.'#13#10'¿Desea continuar?', mbConfirmation, MB_YESNO) = IDYES then begin
			retVal:=postmessage(winHwnd,WM_CLOSE,0,0);
			if retVal then
				Result := True
			else
				Result := False;
		end
		else
			Result := False;
  except
  end;
end;

#ifdef UNICODE
  #define AW "W"
#else
  #define AW "A"
#endif
type
  TDriveType = (
    dtUnknown,
    dtNoRootDir,
    dtRemovable,
    dtFixed,
    dtRemote,
    dtCDROM,
    dtRAMDisk
  );
  TDriveTypes = set of TDriveType;

function GetDriveType(lpRootPathName: string): UINT;
  external 'GetDriveType{#AW}@kernel32.dll stdcall';
function GetLogicalDriveStrings(nBufferLength: DWORD; lpBuffer: string): DWORD;
  external 'GetLogicalDriveStrings{#AW}@kernel32.dll stdcall';

var
  DirCombo: TNewComboBox;

#ifndef UNICODE
function IntToDriveType(Value: UINT): TDriveType;
begin
  Result := dtUnknown;
  case Value of
    1: Result := dtNoRootDir;
//    2: Result := dtRemovable;
    3: Result := dtFixed;
    4: Result := dtRemote;
    5: Result := dtCDROM;
    6: Result := dtRAMDisk;
  end;
end;
#endif

function GetLogicalDrives(Filter: TDriveTypes; Drives: TStrings): Integer;
var
  S: string;
  I: Integer;
  J: Integer;
  K: Integer;
  DriveRoot: string;
begin
  Result := 0;

  I := GetLogicalDriveStrings(0, #0);
  if I > 0 then
  begin
    S := 'CDEFGHIJKLMNOPQRSTUVWXYZ';
 //   SetLength(S, I);
 //   if GetLogicalDriveStrings(Length(S), S) > 0 then
   if Length(S) > 1 then
    begin
      S := TrimRight(S) + #0;
      I := Pos(#0, S);
      J := 1;
      while J <= I do
      begin
//        DriveRoot := Copy(S, 1, I - 1);
        DriveRoot := Copy(S, j, 1) + ':\';

//        #ifdef UNICODE
//        if (Filter = []) or
//          (TDriveType(GetDriveType(DriveRoot)) in Filter) then
//        #else
//        if (Filter = []) or
//          (IntToDriveType(GetDriveType(DriveRoot)) in Filter) then
//        #endif
        K := GetDriveType(DriveRoot);
        if (K <> 1) then   // desconocido ?
        begin
          Drives.Add(DriveRoot);
        end;
//        Delete(S, 1, I);
//        I := Pos(#0, S);
        J := J + 1;
      end;
      Result := Drives.Count;
    end;
  end;
end;

procedure DriveComboChange(Sender: TObject);
begin
  WizardForm.DirEdit.Text := DirCombo.Text;
end;

procedure InitializeWizard;
var
  I: Integer;
  StringList: TStringList;
begin
  StringList := TStringList.Create;
  try
    if GetLogicalDrives([dtFixed], StringList) > 0 then
    begin
      WizardForm.DirEdit.Visible := False;
      WizardForm.DirBrowseButton.Visible := False;

      DirCombo := TNewComboBox.Create(WizardForm);
      DirCombo.Parent := WizardForm.DirEdit.Parent;
      DirCombo.SetBounds(WizardForm.DirEdit.Left, WizardForm.DirEdit.Top,
        WizardForm.DirBrowseButton.Left + WizardForm.DirBrowseButton.Width -
        WizardForm.DirEdit.Left, WizardForm.DirEdit.Height);
      DirCombo.Style := csDropDownList;
      DirCombo.OnChange := @DriveComboChange;

      for I := 0 to StringList.Count - 1 do
        DirCombo.Items.Add(StringList[I] + '{#SetupSetting('DefaultDirName')}');

      DirCombo.ItemIndex := 0;
      DirCombo.OnChange(nil);
    end;
  finally
    StringList.Free;
  end;
end;

Function NextButtonClick(PageId: Integer): Boolean;
var Ruta: string;
begin
    Result := True;
    if (PageId = wpSelectDir) then begin
       Ruta := ExpandConstant('{app}') + '{#ChkBase}';
       if ( FileExists(Ruta) ) then begin
		      MsgBox('Hay al menos un usuario conectado a "{#MyAppName}", debe cerrarlo antes de continuar con la actualización.', mbError, MB_OK);
          Result := False;
          exit;
       end;
    end;
end;

procedure RegisterPreviousData(PreviousDataKey: Integer);
begin
	SetPreviousData(PreviousDataKey, 'LastDir', WizardForm.DirEdit.Text);
end;

[LangOptions]
LanguageName=Spanish
LanguageID=$340A
